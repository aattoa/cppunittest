include(CTest)

# Set the post-build script path here so it can be retrieved from anywhere by `cppunittest_add_tests`.
set(CPPUNITTEST_POST_BUILD_SCRIPT "${CMAKE_CURRENT_LIST_DIR}/cppunittest_add_tests.cmake")

function (cppunittest_add_tests target)
    cmake_parse_arguments(PARSE_ARGV 1 "" "" "" "WORKING_DIR")

    # Prepare arguments for the post-build script.
    if (_WORKING_DIR)
        set(WORKING_DIR "${_WORKING_DIR}")
    else ()
        set(WORKING_DIR "${CMAKE_CURRENT_BINARY_DIR}")
    endif ()
    set(EXECUTABLE "$<TARGET_FILE:${target}>")
    set(CTEST_INCLUDE "${target}-cppunittest-include.cmake")

    # Register the post-build script.
    add_custom_command(
        TARGET "${target}" POST_BUILD
        COMMAND "${CMAKE_COMMAND}"
            "-D" "EXECUTABLE=${EXECUTABLE}"
            "-D" "CTEST_INCLUDE=${CTEST_INCLUDE}"
            "-D" "WORKING_DIR=${WORKING_DIR}"
            "-P" "${CPPUNITTEST_POST_BUILD_SCRIPT}"
        VERBATIM)

    # Tell CTest to include the test script which will be generated by the post-build script.
    set_property(DIRECTORY APPEND PROPERTY TEST_INCLUDE_FILES "${CTEST_INCLUDE}")

    # Link the target against cppunittest for test driver functionality.
    target_link_libraries("${target}" PRIVATE cppunittest)
endfunction ()
